#!/usr/bin/perl

# Summarize gdb backtrace output

use strict;

my %uniqstacks = ();
my $pid;

while(<>) { 
  chomp();
  if ( /^Thread \d+/) { 
    my @frames = ();
    while(<>) { 

      chomp();
      if (/__kernel_vsyscall/) { 
        # skip this frame, the actual syscall is below it
      }
      elsif (/No core file now/) { 
        # no-care
      }
      elsif (/#\d+\s+0x[0-9a-fA-F]+ in (\S+)/) { 
        push @frames, $1
      }
      elsif (/#\d+\s+([a-zA-Z]+)/) { 
        push @frames, $1
      }   
      # #3  <signal handler called>
      elsif (/#\d+\s+(\<signal handler called\>.*)/) { 
        push @frames, $1
      }
      elsif (/(?:^$|Detaching from program.*process (\d+))/) { 
        # end of a thread
        my $line = join('>', @frames);
        if (defined($1)) { 
          $pid = $1;
        }
        if (!defined $uniqstacks{$line}) { 
          $uniqstacks{$line} = 1;
        }
        else { 
            
          $uniqstacks{$line} = $uniqstacks{$line} + 1;
        }
        last;
      }
      elsif (/\s+at/) {  
          # XXX continuation line 
      }
      else { 
        die "teach me about '$_'"; 
      }
    }
  }
}

my ($k, $v);
print "Backtraces for PID $pid\n";
while (($k, $v) = each(%uniqstacks)) {
   print "$v: $k\n";
}


